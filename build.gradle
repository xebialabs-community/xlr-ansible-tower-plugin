plugins {
    id "com.github.hierynomus.license" version "0.14.0"
    id "com.github.hierynomus.jython" version "0.8.0"
    id "com.xebialabs.xl.docker" version "1.1.0"
    id "nebula.release" version "6.0.0"
    id "java"
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'


xlDocker {
  compileImage = 'xebialabs/xlr_dev_compile'
  compileVersion = 'latest'
  runImage = 'xebialabs/xlr_dev_run'
  runVersion = 'latest'
  runPortMapping = '15516:5516'
}

repositories {
    mavenCentral()
}

defaultTasks 'build'

if (!project.hasProperty('release.scope')) {
  project.ext['release.scope'] = 'patch'
}

if (!project.hasProperty('release.useLastTag')) {
  project.ext['release.useLastTag'] = true
}

sourceSets.main.resources.srcDirs += ["src/main/jython"]
sourceSets.test.resources.srcDirs += ["src/test/jython"]

dependencies {
    compile 'de.schlichtherle.truezip:truezip-file:7.7.10'
    jython ":ansible-tower-cli:3.2.1:tower_cli"
    jython ":click:6.7"
    jython ":colorama:0.3.9"
    jython python(":six:1.11.0") {
        useModuleName = false
        copy {
            from "six.py"
        }
    }
    jython python(":pyyaml:3.12") {
        useModuleName = false
        copy {
            from "PyYAML-3.12/lib/yaml"
            into "yaml"
        }
    }
}

project.ext {
    jythonInterpreter = "jython"
}

task runTest(type: Exec, dependsOn: ['testClasses']) {
    environment = ["itest_conf_file": "$projectDir/itest-conf.json", "PATH": System.getenv("PATH"), "HOME": System.getenv("HOME")]
    commandLine jythonInterpreter, "-m", "unittest", "-v", System.getProperty("testName")
    workingDir = "$projectDir/src/test/jython"
}

task itests(type: Exec, dependsOn: ['testClasses']) {
    logger.info("{$sourceSets.test.runtimeClasspath.asPath}")
    environment = ["itest_conf_file": "$projectDir/itest-conf.json", "PATH": System.getenv("PATH"), "HOME": System.getenv("HOME")]
    commandLine jythonInterpreter, "-Dpython.path={$sourceSets.test.runtimeClasspath.asPath}:{$sourceSets.main.runtimeClasspath.asPath}", "-m", "unittest", "discover", "-s", "./src/test/jython", "-p", "*_test.py", "-v"
    workingDir = projectDir
}

license {
    header rootProject.file('License.md')
    strictCheck true
    ignoreFailures false
    excludes(["**/*.json", "**/requests/*","**/tower_cli/*","**/yaml/*","**/click/*", "**/colorama/*", "**/responses/**","**/logback-test.xml","**/six.py"])
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'XEBIALABS'
}

